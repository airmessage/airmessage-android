plugins {
	id "com.android.application"
	id "kotlin-android"
	id "kotlin-parcelize"

	id "com.google.gms.google-services"
	id "com.google.firebase.crashlytics"

	id "com.mikepenz.aboutlibraries.plugin"

	id "com.google.secrets_gradle_plugin" version "0.5"
}

secrets {
	propertiesFileName "secrets.properties"
	defaultPropertiesFileName = "secrets.default.properties"
}

//Load shared keystore
def sharedKeystoreFile = rootProject.file("signingshared/shared.keystore")

//Load keystore
def keystorePropertiesFile = rootProject.file(".signing/keystore.properties")
def keystoreProperties = null
if(keystorePropertiesFile.exists()) {
	keystoreProperties = new Properties()
	keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}


android {
	signingConfigs {
		if(sharedKeystoreFile.exists()) {
			shared {
				storeFile sharedKeystoreFile
				storePassword "android"
				keyAlias "androiddebugkey"
				keyPassword "android"
			}
		}

		if(keystoreProperties) {
			release {
				storeFile file(keystoreProperties["storeFile"])
				storePassword keystoreProperties["storePassword"]
				keyAlias keystoreProperties["keyAlias"]
				keyPassword keystoreProperties["keyPassword"]
			}
		}
	}
	compileSdkVersion 30
	defaultConfig {
		applicationId "me.tagavari.airmessage"
		minSdkVersion 23
		targetSdkVersion 30
		versionName "3.1.7"
		versionCode 113

		resConfigs "en", "fr", "ja"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}
	buildTypes {
		debug {
			ext.enableCrashlytics = false

			applicationIdSuffix ".debug"
			versionNameSuffix "-DEBUG"

			resValue "string", "applicationId", "me.tagavari.airmessage.debug"

			//Use shared, or default to debug if unavailable
			if(sharedKeystoreFile.exists()) {
				signingConfig signingConfigs.shared
			}

			/* minifyEnabled true
			shrinkResources true

			useProguard true
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro" */
		}
		release {
			/* minifyEnabled true
			shrinkResources true

			useProguard true
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro" */

			resValue "string", "applicationId", "me.tagavari.airmessage"

			if(keystoreProperties) {
				signingConfig signingConfigs.release
			}
		}
	}
	lintOptions {
		abortOnError false
	}
	aaptOptions {
		noCompress "tflite"
	}
	productFlavors {
	}
	packagingOptions {
		exclude "META-INF/DEPENDENCIES"
		exclude "META-INF/LICENSE"
	}
	dexOptions {
		javaMaxHeapSize "5g"
	}
	testOptions {
		unitTests.includeAndroidResources = true

	}
	compileOptions {
		coreLibraryDesugaringEnabled true
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = JavaVersion.VERSION_1_8.toString()
	}
}

dependencies {
	coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"

	//implementation fileTree(include: ["*.jar"], dir: "libs")
	testImplementation "junit:junit:4.13.2"
	testImplementation "com.google.truth:truth:1.1.2"
	testImplementation "com.google.truth.extensions:truth-java8-extension:1.1.2"
	testImplementation "androidx.test:core:1.3.0"
	testImplementation "org.mockito:mockito-core:3.6.28"

	androidTestImplementation "androidx.test.ext:truth:1.3.0"
	androidTestImplementation "androidx.test:runner:1.3.0"
	androidTestImplementation "androidx.test:rules:1.3.0"

	//Android extensions
	implementation "androidx.core:core-ktx:1.3.2"
	implementation "androidx.preference:preference-ktx:1.1.1"
	implementation "androidx.browser:browser:1.3.0"
	implementation "androidx.exifinterface:exifinterface:1.3.2"
	implementation "androidx.security:security-crypto:1.0.0"

	//Material components
	implementation "com.google.android.material:material:1.3.0"

	//Android architecture components
	def lifecycle_version = "2.3.1"

	// ViewModel
	implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
	// LiveData
	implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
	// Lifecycles only (without ViewModel or LiveData)
	implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

	//ReactiveX
	implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
	implementation "io.reactivex.rxjava3:rxjava:3.0.12"
	implementation "io.reactivex.rxjava3:rxkotlin:3.0.1"

	//Layouts and views
	implementation "androidx.constraintlayout:constraintlayout:2.0.4"
	implementation "androidx.viewpager2:viewpager2:1.0.0"
	implementation "com.google.android.flexbox:flexbox:3.0.0"

	implementation "nl.dionsegijn:konfetti:1.3.2"
	implementation "com.github.chrisbanes:PhotoView:2.3.0"

	//ML Kit
	implementation "com.google.mlkit:smart-reply:16.1.1"

	//Firebase
	implementation platform("com.google.firebase:firebase-bom:28.0.1")

	implementation "com.google.firebase:firebase-core"
	implementation "com.google.firebase:firebase-messaging-ktx"
	implementation "com.google.firebase:firebase-auth-ktx"
	implementation "com.google.firebase:firebase-analytics-ktx"
	implementation "com.google.firebase:firebase-crashlytics-ktx"

	//GMS
	implementation "com.google.android.gms:play-services-maps:17.0.1"
	implementation "com.google.android.gms:play-services-location:18.0.0"
	implementation "com.google.android.gms:play-services-auth:19.0.0"

	//About libraries
	implementation "com.mikepenz:aboutlibraries-core:$aboutlibs_version"
	implementation "com.mikepenz:aboutlibraries:$aboutlibs_version"

	//Tools
	implementation "com.googlecode.ez-vcard:ez-vcard:0.11.2"

	//Backend
	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation "org.bouncycastle:bcprov-jdk15to18:1.68"

	implementation "com.github.bumptech.glide:glide:4.12.0"
	annotationProcessor "com.github.bumptech.glide:compiler:4.12.0"
	implementation "jp.wasabeef:glide-transformations:4.3.0"

	implementation "com.google.android.exoplayer:exoplayer-core:2.14.0"
	implementation "com.google.android.exoplayer:exoplayer-ui:2.14.0"

	implementation "org.jsoup:jsoup:1.13.1"

	implementation "com.klinkerapps:android-smsmms:5.2.6"

	implementation "org.java-websocket:Java-WebSocket:1.5.2"
	implementation "com.otaliastudios:transcoder:0.10.3"
}

repositories {
	maven {
		url "https://maven.google.com"
		// Alternative URL is "https://dl.google.com/dl/android/maven2/"
	}
	maven {
		url "https://jitpack.io"
	}
}